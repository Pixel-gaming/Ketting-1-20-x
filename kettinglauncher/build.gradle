import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'maven-publish'
    id 'com.github.ben-manes.versions'
    id 'org.javamodularity.moduleplugin' version '1.8.3' apply false
}
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'org.javamodularity.moduleplugin'

repositories {
    mavenCentral()
    maven { url 'https://repo1.maven.org/maven2' }
}

dependencyUpdates.rejectVersionIf { isNonStable(it.candidate.version) }

apply from: rootProject.file('build_shared.gradle')

java {
    toolchain.languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
    withSourcesJar()
}

dependencies {
    implementation project(':fmlcore')
    implementation project(':kettingcommonimpl')
    implementation(libs.yaml)
    implementation(libs.securemodules)
    implementation(libs.gson)
    implementation(libs.guava)
    implementation(libs.terminalcolors)
    implementation 'dev.vankka:dependencydownload-runtime:1.3.1'
    implementation 'dev.vankka:dependencydownload-common:1.3.1'
    implementation 'me.tongfei:progressbar:0.10.0'

    implementation BINPATCH_TOOL
    implementation INSTALLER_TOOLS
    implementation JAR_SPLITTER
    implementation FART
}

jar{
    from project(':kettingcommonimpl').project.sourceSets.main.output
    manifest{
        attributes 'Main-Class': 'org.kettingpowered.ketting.KettingLauncher'
    }
}

ext{
    mainClass = 'org.kettingpowered.ketting.KettingLauncher'
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        from components.java
        artifactId = 'kettinglauncher'
        pom {
            name = project.name
            description = 'Launcher for the Ketting server'
            url = "https://github.com/kettingpowered/Ketting-1-20-x"
            developers {
                developer {
                    id = "justred23"
                    name = "JustRed23"
                }
                developer {
                    id = "c0d3m4513r"
                    name = "C0D3 M4513R"
                }
            }
            scm {
                connection = "scm:git:https://github.com/kettingpowered/Ketting-1-20-x.git"
                //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
                url = "https://github.com/kettingpowered/Ketting-1-20-x"
            }
            PomUtils.setGitHubDetails(pom, 'kettingpowered', 'Ketting-1-20-x')
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/Ketting-1-20-x")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://nexus.c0d3m4513r.com/repository/Ketting-Server-Releases/"
        }
    }
}
